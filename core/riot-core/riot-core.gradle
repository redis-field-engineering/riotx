dependencies {
    api 'org.slf4j:slf4j-api'
    implementation 'org.slf4j:slf4j-simple'
    api group: 'info.picocli', name: 'picocli', version: picocliVersion
    annotationProcessor group: 'info.picocli', name: 'picocli-codegen', version: picocliVersion
    implementation 'org.springframework.boot:spring-boot'
    implementation group: 'me.tongfei', name: 'progressbar', version: progressbarVersion
    api project(':spring-batch-redis-infrastructure')
    testImplementation 'org.awaitility:awaitility'
}

project.rootProject.gradle.addBuildListener(new BuildAdapter() {
    @Override
    void projectsEvaluated(Gradle gradle) {
        gradle.rootProject.subprojects
            .find { p -> p.name == 'riot-core' }
            .processResources {
                inputs.property('build_date', gradle.rootProject.config.buildInfo.buildDate + ':' + gradle.rootProject.config.buildInfo.buildTime)
                filesMatching(['**/RiotVersion.properties']) {
                    expand(
                        'riot_version': gradle.rootProject.version,
                        'build_date': gradle.rootProject.config.buildInfo.buildDate,
                        'build_time': gradle.rootProject.config.buildInfo.buildTime,
                        'build_revision': gradle.rootProject.config.buildInfo.buildRevision
                    )
                }
            }
    }
})