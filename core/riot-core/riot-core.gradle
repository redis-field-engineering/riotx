dependencies {
    api project(':spring-batch-redis-core')
    api project(':spring-batch-memcached')
    api 'org.springframework.boot:spring-boot-autoconfigure'
    api 'com.fasterxml.jackson.core:jackson-databind'
    api group: 'com.tdunning', name: 't-digest', version: tdigestVersion
    api group: 'org.latencyutils', name: 'LatencyUtils', version: latencyUtilsVersion
    api 'org.springframework.batch:spring-batch-infrastructure'
    api 'io.micrometer:micrometer-registry-prometheus'
    api 'io.prometheus:prometheus-metrics-exporter-httpserver'
}

project.rootProject.gradle.addBuildListener(new BuildAdapter() {
    @Override
    void projectsEvaluated(Gradle gradle) {
        gradle.rootProject.subprojects
                .find { p -> p.name == 'riot-core' }
                .processResources {
                    inputs.property('build_date', gradle.rootProject.config.buildInfo.buildDate + ':' + gradle.rootProject.config.buildInfo.buildTime)
                    filesMatching(['**/RiotVersion.properties']) {
                        expand(
                                'riot_version': gradle.rootProject.version,
                                'build_date': gradle.rootProject.config.buildInfo.buildDate,
                                'build_time': gradle.rootProject.config.buildInfo.buildTime,
                                'build_revision': gradle.rootProject.config.buildInfo.buildRevision
                        )
                    }
                }
    }
})
